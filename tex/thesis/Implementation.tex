\chapter{Implementation in Coq}
\label{chap:implementation}

\lstset{style=coq}

We outline some pearls of the implementation of the framework of this thesis in the theorem prover Coq.  We do not assume additional axioms.  The
implementation compiles with the Coq versions 8.7 and 8.8.  We use proof scripts to derive proof terms, using Coq's standard tactic language
\textit{ltac}.  The complete the source code can be downloaded from the homepage of this thesis:
\begin{center}
  \url\homepage
\end{center}


\section*{Used Libraries}
\label{sec:coq-libraries}

We make use of the \textit{typeclass} mechanism built into Coq.  Typeclasses are first-citizen objects in Coq, that may be parametrised over types and
other typeclasses.  Coq uses proof-search to infer instances of typeclasses.  For that, the user has to declare definitions or lemmas as typeclass
instances.  We refer to Cast{\'e}ran and Sozeau~\cite{casteran2012gentle}, for a more thorough description of this feature.

\paragraph{Base Library and Finite Types}

We use a modified version of the library for the lecture \textit{Introduction to Computational Logic} at Saarland University.  This library extends
the standard library of Coq with additional functions and automation for lists and decidable predicates.  On top of this library, we use the library
of finite types that was developed by J.~Menz in his bachelor's thesis \cite{JanMenz}.  The following listing outlines the definition of decidable
predicates and finite types:

\begin{lstlisting}
Definition dec (P:Prop) := {P} + {~ P}.
Definition eq_dec (X:Type) := forall x l, dec (x=l).
Structure eqType := EqType {
  eqtype :> Type;
  decide_eq : eq_dec eqtype
}.
Canonical Structure eqType_CS X (A: eq_dec X) := EqType X.

Class finTypeC (type: eqType) : Type := FinTypeC {
  enum : list type;
  enum_ok : forall x : type, count enum x = 1
}.
Structure finType : Type := FinType {
  type :> eqType;
  class : finTypeC type
}.
Canonical Structure finType_CS (X:Type) {p : eq_dec X} {class : finTypeC (EqType X)} : finType := FinType (EqType X).
\end{lstlisting}%

The coercions (\lstinline!:>!) make it possible to use finite types \lstinline{L:finType} as types, i.e.\ we can write \lstinline!x:L!.  The canonical
structures enable automatic inference of the structure, for example there is a function \lstinline!index : forall L:finType, L -> nat!, and we can
write \lstinline!index true!.

\paragraph{Retraction Library}

We implemented a library for retractions.  We use a typeclass for the existence of retractions between two types.
\begin{lstlisting}
Section Retract.
  Variable X Y : Type.
  Definition retract (f : X -> Y) (g : Y -> option X) :=
    forall x l, g l = Some x <-> l = f x.
  Class Retract := {
    Retr_f : X -> Y;
    Retr_g : Y -> option X;
    Retr_retr : retract Retr_f Retr_g;
  }.
End Retract.
Arguments Retr_f { _ _ _ }.
Arguments Retr_g { _ _ _ }.
\end{lstlisting}
The \textit{Vernacular} command \lstinline!Arguments! makes $X$, $Y$, and the instance contextual implicit.  This means that
\lstinline!Retr_f : X -> Y!, where \lstinline!X! and \lstinline!Y! are inferred from the context.  We define instances for the retractions in
Section~\ref{sec:retracts}.

The retraction composition operator is not defined as an instance, to avoid diverging proof search during the typeclass inference, because it can be
applied arbitrary many times.  Although it is not declared as instance, the retraction composition function \lstinline!ComposeRetract! can be manually
applied:
\begin{lstlisting}
ComposeRetract : forall A B C : Type, Retract B C -> Retract A B -> Retract A C
\end{lstlisting}
We embed composition of retractions into retraction operators.  For example, instead of defining a retraction $RetrLft : X \hookrightarrow X+Y$, we
define (and declare as an instance) an operator on retractions:
\begin{lstlisting}
Retract_inl : forall A B C : Type, Retract A B -> Retract A (B + C)
\end{lstlisting}


\paragraph{Inhabited Types}
We also have a library for inhabited types.  Whenever we need a semantically irrelevant value, we can just write \lstinline!default! and Coq infers
and inserts a value with the following typeclass:
\begin{lstlisting}
Class inhabitedC (X : Type) := {
  default : X;
}.
\end{lstlisting}

\paragraph{Vectors and $\Fin_k$}

We use the type constructors \lstinline!Vector.t! and \lstinline!Fin.t! from Coq's standard library.  However, many basic lemmas and functions are
missing for this type.  We have a small library that adds the missing functions and lemmas.  It also provides some tactics, for example to make case
distinctions over \lstinline!Fin.t (S n)!.  We also introduce notations for elements of $\Fin_k$.

\paragraph{smpl}

We use Sigurd~Schneider's smpl plugin\footnote{\url{https://github.com/sigurdschneider/smpl}}.  It lets the user add tactics to a database (similar to
\lstinline!HintDb!) and provides a tactic that applies the first applicable tactic from the database.  We use this plugin for proof automation, more
on that below.


\section*{Mechanisation of encodable types}
\label{sec:coq-values}

The Coq implementation of the definition of multi-tape Turing machines is straight-forward.  Therefore, we omit code-listings here.  The interested
reader of the PDF version of this thesis may click on the definitions or lemmas, to get to the corresponding Coq code.

We also use typeclasses to implement the notion of encodable types:\footnote{Note that for technical reasons we do not parametrise \lstinline!codable!
  over \lstinline!sig:finType!.}

\begin{lstlisting}
Class codable (sig: Type) (X: Type) := {
  encode : X -> list sig;
}.
Arguments encode {sig} {X} {_}.
Coercion encode : codable >-> Funclass.
\end{lstlisting}

When \lstinline!cX : codable sig X!, the above \lstinline!Coercion! vernacular makes it possible to write \lstinline!cX x!.  This applies the concrete
encoding function given by the instance \lstinline!cX!.  We prefer this notation over \lstinline!encode x!, due to the fact that encodability is
ambiguous, as noted in Section~\ref{sec:value-containment}.  We define the alphabets in Definition~\ref{lem:retracts-basic} inductively, and also
declare their proofs of finiteness.

We realise the type of the extended alphabet $\Sigma^+ := boundary + \Sigma$, where
\[ boundary ::= \MS{START} ~|~ \MS{STOP} ~|~ \MS{UNKNOWN} \]%
is defined as an inductive type.


\section*{Automation}
\label{sec:coq-automation}

Our proof scripts make use of the feature of \textit{existential variables}.  An existential variable $?X$ is a type (that may contain variables
referring to more existential variables) and the environment in which it should be typed.  Existential variables are refined during unification (e.g.\
using the tactic ``\lstinline!apply!'') and created with tactics like ``\lstinline!eapply!''.  Before the proof script can be finished and saved with
the \lstinline!Qed! command, all existential variables must be bound to values.

Our tactic \lstinline!TMSimp! destructs conjunctive assumptions in all hypotheses (i.e.\ logical conjunctions and logical existentials).  It also
introduces and names tapes to \lstinline!tmid!, \lstinline!tmid0!, etc.  Furthermore, it instantiates and rewrites with hypotheses of the form
$H: \forall j.~ j \notin I \rightarrow tmid0[j] = tmid[j]$, that come from the correctness Lemma~\ref{lem:LiftTapes_Realise} of the tapes-lift
operator.

The tactic \lstinline!modpon H! instantiates assumptions of the form $H: \forall x~\cdots.~P \rightarrow \cdots \rightarrow Q$.  For each premise that
the tactic could not solve automatically, it creates a new subgoal with existential variables for the quantified variables.

The tactic \lstinline!TM_Correct! instantiates the existential variable for the relation $?R'$ and proves $M \Realise ?R'$.  For that, it applies all
correctness lemmas of the primitive machines, control-flow operators, lifts, constructors and deconstructors, and some other auxiliary machines.  The
user can declare more correctness and running time lemmas to be used by \lstinline!TM_Correct!.  For that, we use the Coq plugin \textit{smpl}.  For
example, the following code declares the correctness and running time lemmas of \lstinline!LiftTapes!:

\begin{lstlisting}
Ltac smpl_TM_LiftN :=
  lazymatch goal with
  | [ |- LiftTapes _ _ $\Realise$ _] =>
    apply LiftTapes_Realise; [ smpl_dupfree | ]
  | [ |- LiftTapes _ _ $\Realise$c(_) _] =>
    apply LiftTapes_RealiseIn; [ smpl_dupfree | ]
  | [ |- projT1 (LiftTapes _ _) $\TerminatesIn$ _] =>
    apply LiftTapes_Terminates; [ smpl_dupfree | ]
  end.
Smpl Add smpl_TM_LiftN : TM_Correct.
\end{lstlisting}
The tactic \lstinline!smpl_dupfree! automatically proves that a vector is duplicate-free.

Note that we do not register correctness lemmas to \lstinline!TM_Correct! that require semantical information.  For example, the correctness
Lemmas~\ref{lem:Reset_Realise} (\lstinline!Reset_Realise!) and~\ref{lem:CopyValue_Realise} (\lstinline!CopyValue!) are parametrised over the encoding
of $X$.  The user has to applying the lemmas with the correct encoding manually.


\section*{Example Correctness Proof in Coq}
\label{sec:coq-correctness}

We use the general approach how to prove correctness (or running time) of a Turing machine, as described in Chapter~\ref{chap:compound}.  For example,
consider the following goal (we use the user-defined Coq notation \lstinline!$\Realise$c(k)! to mean $\RealiseIn{k}$).
\begin{lstlisting}
============================
Add_Step $\Realise$c(9) Add_Step_Rel
\end{lstlisting}
The outline of the proof script is:
\begin{lstlisting}
Proof.
  eapply RealiseIn_monotone.
  { unfold Add_Step. TM_Correct. }
  { cbn. reflexivity. }
  { (* ... *) }
Qed.
\end{lstlisting}

The tactic \lstinline!eapply RealiseIn_monoton! applies Lemma~\ref{lem:Realise_monotone}, and creates existential variables $?R'$ and $?k'$ and three
subgoals:
\begin{lstlisting}
3 focused subgoals
(shelved: 2)
  
============================
Add_Step $\Realise$c(?k') ?R'

subgoal 2 is:
 ?k' <= 9
subgoal 3 is:
 ?R' <<=2 Add_Step_Rel
\end{lstlisting}

After focusing the first subgoal, we unfold the definition of \lstinline!Add_Step!:
\begin{lstlisting}
============================
If (LiftTapes CaseNat [|Fin1|])
   (Return (LiftTapes Constr_S [|Fin0|]) None)
   (Return Nop (Some tt)) $\Realise$(?k') ?R'
\end{lstlisting}

The tactic \lstinline!TM_Correct! automatically applies the correctness lemmas of the conditional, tape-lifting, etc.  It instantiates
\lstinline!?R'! and \lstinline!k'!:
\begin{lstlisting}
?R' := (LiftTapes_Rel [|Fin1|] CaseNat_Rel|_true $\circ$
        ($\bigcup_{f:\Unit}$ LiftTapes_Rel [|Fin0|] S_Rel|_f)||_None)
       $\cup$
       (LiftTapes_Rel [|Fin1|] CaseNat_Rel|_false $\circ$
        ($\bigcup_{f:\Unit}$ Nop_Rel|_f)||_(Some tt))
?k' := 1 + CaseNat_steps + Nat.max Constr_S_steps 0
\end{lstlisting}
where \lstinline!CaseNat_steps! is the constant number of steps required for $\MS{CaseNat}$ (i.e.\ $5$) and \lstinline!Constr_S_steps! is $3$.  By
simplification, the second goal reduces to \lstinline!9<=9!, which is solved by reflexivity of $\leq$.

The main part of the proof is the third goal (with \lstinline!R'! substituted).  We prove it with the following proof script:
\begin{lstlisting}
{
  intros tin (yout, tout) H. cbn. intros a b HEncA HEncB.
  cbn in *. destruct H; TMSimp.
  - modpon H. destruct b; auto.
  - modpon H. destruct b; auto.
}
\end{lstlisting}

After the introduction of the variables and hypotheses \lstinline!HEncA: tin[@Fin0] $\simeq$ a!, \lstinline!HEncB: tin[@Fin1] $\simeq$ b!, and
\lstinline!H: R' tin (yout, tout)!, we make a case-distinction over \lstinline!H! (note that the head symbol in the definition of \lstinline!R'! is
$\cup$, so \lstinline!H! reduces to a disjunction).  This gives two sub-goals.  In both subgoals, we use the automation tactic \lstinline!TMSimp!.
The first subgoal is:
\begin{lstlisting}
tin, tout, tmid : tapes (boundary + sigNat) 2
H : forall n : nat, tin[@Fin1] $\simeq$ n -> match n with
                               | 0 => False
                               | S n' => tmid[@Fin1] $\simeq$ n'
                               end
H0 : forall n : nat, tin[@Fin0] $\simeq$ n -> tout[@Fin0] $\simeq$ S n
a, b : nat
HEncA : tin[@Fin0] $\simeq$ a
HEncB : tin[@Fin1] $\simeq$ b
H0_0 : tmid[@Fin0] = tin[@Fin0]
H1_0 : tout[@Fin1] = tmid[@Fin1]
============================
match b with
| 0 => False
| S b' => tout[@Fin0] $\simeq$ S a /\ tmid[@Fin1] $\simeq$ b'
end
\end{lstlisting}

In this case, we know that \lstinline!a! must be equal to \lstinline!S a'! for some \lstinline!a'!.  The assumption \lstinline!H! is automatically
instantiated with \lstinline!a! and the proof \lstinline!HEncA!.  We finish the goal by case-distinction over \lstinline!a!.  The second goal is
analogous.


Even for complex machines, the correctness proofs in Coq follow this pattern.  It is important to note that the structure of the proof always follows
the structure of the machine.  Runtime proofs are analogous.


\section*{Lines of Code}
\label{sec:coq-lines}

In Table~\ref{tab:coq-lines}, we summarise the numbers of Coq code.  We used the tool \texttt{coqwc} to count the lines.  The case-studies are under
the horizontal line in the middle.
\begin{table}[h]
  \centering
  \begin{tabular}{p{6.5cm}|r|r}
    Module                                    & Spec & Proof\\ \hline
    Preliminary (incl. $\Loop$ and relations) &  176 &   84 \\
    Definition of Turing machines             &  430 &  194 \\
    Primitive Machines                        &  122 &   34 \\
    Control-flow operators                    &  425 &  383 \\
    Lifting                                   &  362 &  193 \\
    Simple Machines                           &  380 &  278 \\
    Value containment                         &  394 &  119 \\
    Copying and writing values                &  411 &  288 \\
    Alphabet-Lift with values                 &  133 &  147 \\
    Deconstructors and constructors           &  486 &  482 \\
    Notations and tactics for compound
    or programmed machines                    &  165 &   15 \\  \hline
    $\MS{MapSum}$                             &   47 &  110 \\
    Addition and Multiplication machines      &  181 &  298 \\
    List functions machines                   &  326 &  456 \\
    Heap machine simulator                    &  981 & 1040 \\ \hline\hline
    Total                                     & 5019 & 4121 \\
  \end{tabular}
  \caption{Lines of specification and proof code for the ``modules'' (with several source files)}
  \label{tab:coq-lines}
\end{table}

The total number of library lines is $153$ spec and $2638$ proof.  The total compilation time is circa 4:30~minutes.\footnote{Measured on the
  following hardware: \texttt{Intel(R) Core(TM) i7-4710MQ CPU}; 8 cores @ 2.50GHz; compiled on GNU/Linux with \texttt{make -j8} and Coq 8.8.1.}

Using Coq`s \texttt{Extract} mechanism, we compiled the Coq implementation to Haskell, and we were able to count the total number of states of the
heap machine simulator: The alphabet of $\MS{Loop}$ consists of $30$ symbols and it has $11537$ states.



%%% Local Variables:
%%% TeX-master: "thesis"
%%% End:
